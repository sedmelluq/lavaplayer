plugins {
  id 'java'
  id 'groovy'
  id 'org.sonarqube'
  id 'maven-publish'
  id 'com.jfrog.bintray'
}

ext.moduleName = 'lavaplayer'
version = '1.3.32'

repositories {
  maven {
    // Only used for tests
    url 'https://dl.bintray.com/sedmelluq/com.sedmelluq'
  }
}

dependencies {
  compile 'com.sedmelluq:lava-common:1.1.0'
  compile 'com.sedmelluq:lavaplayer-natives:1.3.13'

  compile "org.slf4j:slf4j-api:$slf4jVersion"

  compile 'org.apache.httpcomponents:httpclient:4.5.10'
  compile 'commons-io:commons-io:2.6'

  compile 'com.fasterxml.jackson.core:jackson-core:2.10.0'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.10.0'

  compile 'org.jsoup:jsoup:1.12.1'

  compile 'net.iharder:base64:2.3.9'

  testCompile 'org.codehaus.groovy:groovy:2.5.5'
  testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'
  testCompile 'ch.qos.logback:logback-classic:1.2.3'
  testCompile 'com.sedmelluq:lavaplayer-test-samples:1.3.11'
}

jar {
  exclude("natives")
}

task updateVersion {
  new File("$projectDir/src/main/resources/com/sedmelluq/discord/lavaplayer/tools/version.txt").with {
    it.parentFile.mkdirs()
    it.text = version
  }
}

classes.finalizedBy(tasks['updateVersion'])

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId rootProject.group
      artifactId moduleName

      from components.java

      artifact sourceJar {
        classifier 'sources'
      }
    }
  }
}

apply from: "${rootDir}/gradle/bintray.gradle"
